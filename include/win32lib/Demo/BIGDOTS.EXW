-- Dots...
-- Demo for Win32Lib by Brian K. Broker
--   Tile dots in window
--   Clicking a dot toggles it's status (filled or empty)

include win32lib.ew
include dot_xmp.ew -- Image Definitions
without warning

constant
  Win = create( Window, "Tile Dots...", 0, Default, Default, 170, 190, 0 ),
  Buffer = create( Pixmap, "", Win, 0, 0, 170, 190, 0 )

constant TileSize = 55

sequence status -- holds status of dots
  -- initialize status so each tile holds a dot
  status = repeat( repeat( FullDot, 3 ), 3 )

------------------------------------------------------------

procedure onMouse_Win(integer self, integer event, sequence parms)
  integer mouse_event,x,y

  mouse_event = parms[1]
  if mouse_event = LeftDown then
    x = parms[2]
    y = parms[3]

    --printf( 1, "Color: 0x%x\n", {getPixel( Win, x, y )} )
    -- convert window coordinates to tile coordinates
    x = floor( x/TileSize + 1 )
    if x > 3 then
      x = 3
    end if
    y = floor( y/TileSize + 1 )
    if y > 3 then
      y = 3
    end if

    printf(1,"Tile %d,%d clicked. ",{x,y})
    -- change status of dot
    if status[x][y] = FullDot then
      status[x][y] = EmptyDot
      puts(1,"Status: Empty\n" )
    else
      status[x][y] = FullDot
      puts(1,"Status: Full\n" )
    end if
    -- update Buffer and Window
    copyBlt( Buffer, (x-1)*TileSize, (y-1)*TileSize, status[x][y] )
    copyBlt( Win, (x-1)*TileSize, (y-1)*TileSize, status[x][y] )
  end if
end procedure
setHandler(Win,w32HMouse,routine_id("onMouse_Win"))

------------------------------------------------------------

procedure onPaint_Win(integer self, integer event, sequence parms)
  -- copy Buffer into Window for fast repaint
  
  copyBlt( Win, 0, 0, Buffer )
end procedure
setHandler(Win,w32HPaint,routine_id("onPaint_Win"))

------------------------------------------------------------
                         
procedure onOpen_Win(integer self, integer event, sequence parms)
  -- initialize Buffer
  setPenColor( Buffer, rgb(#D4,#D0,#C8) )
  drawRectangle( Buffer, True, 0, 0, 170, 190 )
  for x = 1 to 3 do
    for y = 1 to 3 do
      copyBlt( Buffer, (x-1)*TileSize, (y-1)*TileSize, status[x][y] )
      printf(1,"Dot %d,%d copied to Buffer.\n", {x,y} )
    end for
  end for
end procedure
setHandler(Win,w32HOpen,routine_id("onOpen_Win"))

-----------------------
-------- start --------
WinMain( Win, Normal )
-----------------------

